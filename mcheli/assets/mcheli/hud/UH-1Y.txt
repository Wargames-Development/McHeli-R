;本文件由TV90及BFMC开发团队制作，禁止盗用！！！

;主背景
DrawTexture = MH60_MainHUD, -500, -250,  1000,500, 0,0, 2048,1024
DrawTexture = RotCross, -500, -285,  1000,500, 0,0, 2048,1024,-roll/2
;低血量警告
If = hp_rto>0.25 && hp_rto<0.75
DrawTexture = AH6JWARN, -41, 28,  86,15, 0,0, 460,80
EndIf
If = hp_rto<=0.25
DrawTexture = AH6JBIGWARN, -74, 28,  150,15, 0,0, 800,80
EndIf

;过热
If = is_heat_wpn==1
	Color = 0xFFD0EA52
	DrawRect = -20, 90, 41, 2
	Color = 0xFFDF0408
	DrawRect = -20, 90, 41*wpn_heat, 2
EndIf

;武器
If = sight_type==0&&reloading==0&&wpn_heat<1.0
Color = 0xD0EA52
DrawCenteredString = 0, 80, "%s",  WPN_NAME
Endif
If = sight_type==0&&(reloading==1||wpn_heat==1.0)
Color = 0xFFDF0408
DrawCenteredString = 0, 80, "%s",  WPN_NAME
Endif

;机身指引
If = hovering==0
DrawTexture = Z11WGuide, -64, -130-plyr_pitch*3,  128,260, 0,0, 256,500,-roll/3
EndIf
If = hovering==1
DrawTexture = Z11WGuide, -64, -130, 128,260, 0,0, 256,500
EndIf

;红外线热诱弹
If = have_flare==1
        DrawTexture = IRFlare, -200, -100, 70,20 ,0,0, 280,80,0
Endif
If = have_flare==1&&can_flare==1
        DrawTexture = AH6JReady, -200, -80, 70,20 ,0,0, 280,80,0
Endif
If = have_flare==1&&can_flare==0
        DrawTexture = NotReady, -200, -80, 70,20 ,0,0, 280,80,0
Endif

;油门
DrawTexture = Z11WArrow, 170, 70-Throttle*160,  20, 20, 0,0, 256,256

;高度
Color = (altitude>10&&altitude<200)? 0xD0EA52: 0xFFDF0408
DrawString = -190, 50, "ALT  %3d", ALTITUDE

;重力
Color = 0xD0EA52
DrawString = -188, -34, "G   %.2f",motion_Y

;加速度
Color = 0xD0EA52
DrawString = 152, -6, "%.2f",THROTTLE

;近地警告
If = altitude<=15&&motion_y<=0&&(pitch>45||pitch<-60)&&free_look ==0
DrawTexture = AH6JLandingWARN, -150, -150, 300,300 ,0,0, 500,500,0
EndIf

;操纵杆
DrawTexture = hud, -16+8+stick_x*6, 92+8+stick_y*6, 16,16, 128,96, 32,32

Color = 0xD0EA52
If = free_look==1
	DrawString = -90, -80, "FREE LOOK"
Endif

If = hovering==1
	Color = 0xD0EA52
	DrawCenteredString = 0, 10, "HOVERING"
Endif
;血量
Color = hp_rto>0.25? 0xD0EA52: #FFFF6A6A
DrawString  = 150,     50, "HP %3.0f", HP_PER